apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["currentSDK"]

    defaultConfig {
        applicationId "com.tiagohs.script_reader"

        minSdkVersion globalConfiguration["minSDK"]
        targetSdkVersion globalConfiguration["currentSDK"]
        versionCode globalConfiguration["versionCode"]
        versionName globalConfiguration["versionName"]

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            keyAlias 'script_reader'
            keyPassword '14789632'
            storeFile file('../keystore/keystore.keystore')
            storePassword '14789632'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    buildTypes {
        debug {
            buildConfigField "String", "ADMOB_APP_ID", '""'
            buildConfigField "String", "ADMOB_APP_BANNER_ID", '"ca-app-pub-00000"'
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true

            multiDexKeepProguard file('multidex-config.pro')
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "String", "ADMOB_APP_ID", '""'
            buildConfigField "String", "ADMOB_APP_BANNER_ID", '"ca-app-pub-00000"'

            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation project(path: ':components')
    implementation project(path: ':helpers')
    implementation project(path: ':entities')
    implementation project(path: ':domain')


    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    def dependencies = rootProject.ext.dependencies

    /**
     * ANDROID SUPPORT Dependencies
     */
    implementation dependencies.appcompat
    implementation dependencies.constraintlayout
    implementation dependencies.browser
    implementation dependencies.material
    implementation dependencies.media
    implementation dependencies.legacy_support
    implementation dependencies.exifinterface
    implementation dependencies.androidAboutPage

    /**
     * View / Components Dependencies
     */

    implementation dependencies.android_pdf_viewer
    implementation dependencies.lottie

    // Picasso
    implementation dependencies.picasso
    implementation dependencies.picasso_transformations

    /**
     * FIREBASE
     */
    implementation dependencies.firebase_analytics
    implementation dependencies.firebase_crashlytics

    /**
     * DAGGER Dependencies
     */
    implementation dependencies.dagger
    annotationProcessor dependencies.dagger_compiler
    kapt dependencies.dagger_compiler
    implementation dependencies.javax_annotation

    /**
     * Network client
     */
    implementation dependencies.okhttp
    implementation dependencies.logging_interceptor

    /**
     * RX JAVA 2 Dependencies
     */
    implementation dependencies.rxjava2_rxandroid
    implementation dependencies.rxjava2_rxjava

    /**
     * REST
     */
    implementation dependencies.retrofit2_retrofit
    implementation dependencies.retrofit2_adapter_rxjava2

    /**
     * GSON
     */
    implementation dependencies.gson

    /**
     *  HTML Parser / Jspoon
     */
    implementation dependencies.jspoon

    /**
     * Ads
     */
    // implementation 'com.google.android.gms:play-services-ads:17.1.2'

    /**
     * TESTS Dependencies
     */
    testImplementation dependencies.junit
    androidTestImplementation dependencies.junit_ext
    androidTestImplementation dependencies.espresso_core
}
